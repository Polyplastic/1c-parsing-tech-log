#Область ЧтениеЖурнала

Процедура ПозиционироватьЧтениеНаСтрокуФайла(ЧтениеФайла, НомерСтроки)
	ТекНомерСтроки = 0;
	ТекСтрокаФайла = "";
	Пока ТекНомерСтроки < НомерСтроки 
		И ТекСтрокаФайла <> Неопределено Цикл
			ТекСтрокаФайла = ЧтениеФайла.ПрочитатьСтроку();
			ТекНомерСтроки = ТекНомерСтроки + 1;
	КонецЦикла; 
КонецПроцедуры

Функция ЗначениеСвойстваБезЭкранирования(ЗначениеСвойства)
	Результат = ЗначениеСвойства;
	Если Лев(ЗначениеСвойства,1)="'" И Прав(ЗначениеСвойства,1)="'" 
		ИЛИ Лев(ЗначениеСвойства,1)="""" И Прав(ЗначениеСвойства,1)="""" Тогда
			Результат = Сред(ЗначениеСвойства,2,СтрДлина(ЗначениеСвойства)-2);	
	КонецЕсли;
	Возврат СокрЛП(Результат);
КонецФункции

Функция ПолучитьВременныеПараметрыПоСвойствамФайла(Знач ФайлТЖ) Экспорт
	
	СвойстваФайла = новый Структура("Год,День,Месяц,Час,ДатаФайла");
	
	СвойстваФайла.Год = Число(Лев(ФайлТЖ.Имя, 2));
	СвойстваФайла.Месяц = Число(Сред(ФайлТЖ.Имя, 3, 2));
	СвойстваФайла.День = Число(Сред(ФайлТЖ.Имя, 5, 2));
	СвойстваФайла.Час = Число(Прав(ФайлТЖ.ИмяБезРасширения, 2));
	СвойстваФайла.ДатаФайла = Дата(СвойстваФайла.Год + 2000, СвойстваФайла.Месяц, СвойстваФайла.День, СвойстваФайла.Час, 0, 0);

	Возврат СвойстваФайла;
	
КонецФункции // ПрочитатьЖурналПоРегистру

#КонецОбласти


#Область Служебные

Функция ИнформационнаяБазаФайловая(Знач СтрокаСоединенияИнформационнойБазы = "") Экспорт
			
	Если ПустаяСтрока(СтрокаСоединенияИнформационнойБазы) Тогда
		СтрокаСоединенияИнформационнойБазы =  СтрокаСоединенияИнформационнойБазы();
	КонецЕсли;
	Возврат Найти(Врег(СтрокаСоединенияИнформационнойБазы), "FILE=") = 1;
	
КонецФункции 

Функция ПолучитьАнализаторНачалаСтроки()
	Возврат АнализаторСоздать("([0-9]{2}):([0-9]{2})\.([0-9]+)\-([0-9]+)\,(\w+)\,(\d+)");
КонецФункции

Функция ПолучитьАнализаторСвойств()
	Возврат АнализаторСоздать(",([^""',=]+)=('[^']+'|""[^""]+""|[^""',]+)");
КонецФункции

Функция ПолучитьАнализаторКавычкиВКавычках()
	Возврат АнализаторСоздать(",([^""',=]+)=""[^""]*""{2}[^""]*""");
КонецФункции


// Создает структуру с даннымы анализатора
//
// Параметры:
//  Выражение - Строка - Строковое регулярное выражение
//
// Результат:
//  Структура - Структура с данными анализатора. Поля структуры:
//  - *Тип - Строка - вид анализатора "VBS", "Lin", Неопределено
//  - *Компонена - Произвольный - COMОбъект для "VBS", ВнешняяКомпонента для "Lin" или Неопределено
//  - *Выражение - Строка - Строковое регулярное выражение
//   
Функция АнализаторСоздать(Выражение)
	
	Анализатор = Новый Структура("Выражение,Тип,Компонента", Выражение);
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	
	Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86
	 ИЛИ СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		Компонента = Новый COMОбъект("VBScript.RegExp");
		Компонента.Global = Истина;
		Компонента.Pattern = Выражение;
		Компонента.IgnoreCase = Истина;
		
		Анализатор.Тип = "VBS";
		Анализатор.Компонента = Компонента;
	Иначе
		Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86 Тогда
			Платформа = "Lin";
			Разрядность = "32";
		ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86_64 Тогда
			Платформа = "Lin";
			Разрядность = "64";
		Иначе
			ВызватьИсключение "Не поддерживаемый тип платформы";
		КонецЕсли;
		Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.23.0") < 0 Тогда
			Местоположение = "ОбщийМакет.RegEx" + Платформа + Разрядность;
			ПодключитьВнешнююКомпоненту(Местоположение, "ВычислительРегВыражений", ТипВнешнейКомпоненты.Native);
			Компонента = Новый("AddIn.ВычислительРегВыражений.RegEx");
			Компонента.IgnoreCase = Истина;
			Компонента.Global = Истина;
			Компонента.MultiLine = Ложь;
			
			Анализатор.Тип = "Addin";
			Анализатор.Компонента = Компонента;
		Иначе
			Анализатор.Тип = "1CV8";
		КонецЕсли;
	КонецЕсли;
	
	Возврат Анализатор;
	
КонецФункции

// Проверяет совпадение строки текста регулярному выражению анализатора
//
// Результат:
//  Булево - Истина, если строка соответствует регулдярному выражени.ю
//
Функция АнализаторПроверить(Анализатор, СтрокаТекста)
	
	Если Анализатор.Тип = "VB" Тогда
		Результат = Анализатор.Компонента.Test(СтрокаТекста);
		
	ИначеЕсли Анализатор.Тип = "Addin" Тогда
		ТекстJSON = Анализатор.Компонента.MatchesJSON(СтрокаТекста, Анализатор.Выражение);
		Результат = ТекстJSON <> "[]";
		
	Иначе
		Совпадение = Вычислить("СтрНайтиПоРегулярномуВыражению(СтрокаТекста, Анализатор.Выражение,,,, Истина, Ложь)");
		Результат = Совпадение.НачальнаяПозиция > 0;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив совпадение групп по регулярному выражению для переданной строки
//
// Параметры:
//  Анализатор - Структура - данные анализатора
//  СтрокаТекста - Строка - строка для анализа
//  КоличествоГрупп - Число - количество групп, которое должно получиться в результате
//
// Результат:
//  Массив - Массив совпадений
//  *Массив - Массив строк - массив с данными групп каждого совпадения
//
Функция АнализаторНайтиВсе(Анализатор, СтрокаТекста, КоличествоГрупп)
	
	Результат = Новый Массив;
	
	Если Анализатор.Тип = "VBS" Тогда
		Совпадения = Анализатор.Компонента.Execute(СтрокаТекста);
		
		Если Совпадения.Count() = 0 Тогда
			Возврат Результат;
		КонецЕсли;
		
		Для Сч = 0 По Совпадения.Count() -1 Цикл
			Группа = Новый Массив;
			Совпадение = Совпадения.Item(Сч).SubMatches;
			Для Ном = 0 По КоличествоГрупп - 1 Цикл
				Группа.Добавить(Совпадение.Item(Ном));
			КонецЦикла;
			Результат.Добавить(Группа);
		КонецЦикла;
		
	ИначеЕсли Анализатор.Тип = "Addin" Тогда
		ТекстJSON = Анализатор.Компонента.MatchesJSON(СтрокаТекста, Анализатор.Выражение);
		
		Если ТекстJSON = "[]" Тогда
			Возврат Результат;
		КонецЕсли;
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТекстJSON);
		Совпадения = ПрочитатьJSON(ЧтениеJSON, Ложь);
		ЧтениеJSON.Закрыть();
		
		Для Сч = 0 По Совпадения.Количество() -1 Цикл
			Группа = Новый Массив;
			Совпадение = Совпадения[Сч].SubMatches;
			Для Ном = 0 По КоличествоГрупп - 1 Цикл
				Группа.Добавить(Совпадение[Ном]);
			КонецЦикла;
			Результат.Добавить(Группа);
		КонецЦикла;
	Иначе
		//@skip-check server-execution-safe-mode
		Совпадения = Вычислить("СтрНайтиВсеПоРегулярномуВыражению(СтрокаТекста, Анализатор.Выражение, Истина, Ложь)");
		
		Если Совпадения.Количество() = 0 Тогда
			Возврат Результат;
		КонецЕсли;
		
		Для Сч = 0 По Совпадения.Count() -1 Цикл
			Группа = Новый Массив;
			Совпадение = Совпадения[Сч].ПолучитьГруппы();
			Для Ном = 0 По КоличествоГрупп - 1 Цикл
				Группа.Добавить(Совпадение[Ном].Значение);
			КонецЦикла;
			Результат.Добавить(Группа);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


#КонецОбласти

#Область ЧтениеВСпарвочникPerfomance


Функция РазобратьФайлВСправочникPerfomanceMonitor(Замер, СтруктураПараметров) Экспорт
	ИмяФайлаДляРазбора 	= СтруктураПараметров.ПолноеИмя;
	ПериодФайла			= СтруктураПараметров.ПериодФайла;
	
	//инициализация фильтров
	РеквизитыЗамера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Замер, "ФильтрТипСобытия,ФильтрСвойстваСобытия,ФильтрСвойстваСобытияКроме,ФильтрДлительность");
	РеквизитыЗамера.Вставить("ФильтрТипСобытия", РеквизитыЗамера.ФильтрТипСобытия.Получить());
	ЕстьФильтрТипСобытия = РеквизитыЗамера.ФильтрТипСобытия<>Неопределено И РеквизитыЗамера.ФильтрТипСобытия.Количество()>0;
	ЕстьФильтрСвойстваСобытия = ЗначениеЗаполнено(РеквизитыЗамера.ФильтрСвойстваСобытия);
	ЕстьФильтрДлительность = ЗначениеЗаполнено(РеквизитыЗамера.ФильтрДлительность); 
	
	ФайлЗамера = Справочники.ФайлыЗамера.ПолучитьФайлПоПолномуИмени(Замер, ИмяФайлаДляРазбора);
	
	//еще раз проверим прочитан полностью
	СостояниеЧтения = РегистрыСведений.СостояниеЧтения.ПолучитьСостояние(ФайлЗамера);
	Если СостояниеЧтения.ЧтениеЗавершено Тогда
		Возврат 0;
	КонецЕсли;		
	
	//пропуск пустых файлов
	ФайлТЖ = Новый Файл(ИмяФайлаДляРазбора);
	//ФС фикс 10.06.2020 проверка наличия файла
	Если НЕ ФайлТЖ.Существует() Тогда
		РегистрыСведений.СостояниеЧтения.УстановитьСостояниеОшибки(ФайлЗамера, "Файл не найден");
		Возврат 0;
	КонецЕсли;
	
	РазмерФайла = ФайлТЖ.Размер();
	Если РазмерФайла <=3 Тогда
		Возврат 0;
	КонецЕсли;
	
	//ПериодФайла = ОбновлениеДанныхРегламентное.ПолучитьПериодПоИмениФайла(ФайлТЖ.ИмяБезРасширения);
	//Процесс = ОбновлениеДанныхРегламентное.ПолучитьПроцессПоИмениФайла(ИмяФайлаДляРазбора);
	Процесс = Неопределено;
	
	ДатаНачалаЧтения = ТекущаяДата();
	
	Текст = Новый ЧтениеТекста(ИмяФайлаДляРазбора, КодировкаТекста.ANSI, Символы.ВК + Символы.ПС, "", Ложь);
	
	// Всегда читаем перую строку для свойств
	СтрокаТекста = Текст.ПрочитатьСтроку();
	// разделение по csv запятая
	МассивИменСвойств = СтрРазделить(СтрокаТекста,",",Ложь); 
	
	// нормализуем параметры
	Для ш=0 по МассивИменСвойств.ВГраница() Цикл
		МассивИменСвойств[ш] = СтрЗаменить(МассивИменСвойств[ш],"""","");
		Если Найти(МассивИменСвойств[ш],"\\") Тогда
			МассивЧастей = СтрРазделить(СтрЗаменить(МассивИменСвойств[ш],"\\",""),"\");
			Если Найти(МассивЧастей[1],":") И Найти(ВРЕГ(МассивЧастей[1]),"SQL") Тогда // #69 только для типов MSSQL разделять
				МассивЧастейСКЛ = СтрРазделить(МассивЧастей[1],":");
				МассивЧастей[1]=СокрЛП(МассивЧастейСКЛ[1]);
			КонецЕсли;
			МассивИменСвойств[ш] = СокрЛП(МассивЧастей[1]+"\"+МассивЧастей[2]);
		КонецЕсли;
	КонецЦикла;
	
	ПозиционироватьЧтениеНаСтрокуФайла(Текст, СостояниеЧтения.ПрочитаноСтрок);

	//продолжаем чтение с позиции СостояниеЧтения.ПрочитаноСтрок
	ПрочитаноСтрок = СостояниеЧтения.ПрочитаноСтрок;
	СтрокаТекста = Текст.ПрочитатьСтроку();
	Если СтрокаТекста = Неопределено Тогда
		
		//может быть прочитано строк не поменялось а полностью поменялось 
		РегистрыСведений.СостояниеЧтения.УстановитьСостояние(
			ФайлЗамера, 
			ПериодФайла,
			ПрочитаноСтрок, 
			ДатаНачалаЧтения, 
			РазмерФайла);		
		Возврат 0;
	КонецЕсли;
	ПрочитаноСтрок = ПрочитаноСтрок + 1;
	
	
	//часть реквизитов будет одинакова для всего файла
	СтруктураЗаписи = ПолучитьСтруктуруЗаписиСправочник();
	СтруктураЗаписи.Владелец = Замер;
	СтруктураЗаписи.Файл = ФайлЗамера;
	СтруктураЗаписи.ТипПроцесса = Процесс;
	
	Пока СтрокаТекста <> Неопределено ИЛИ НЕ ЗначениеЗаполнено(СтрокаТекста) Цикл
		
		// Проверяем, является ли следующая строка начальной строкой журнала
		СледующаяСтрока = Текст.ПрочитатьСтроку();
		
		Если СледующаяСтрока = Неопределено ИЛИ НЕ ЗначениеЗаполнено(СледующаяСтрока) Тогда
			Прервать;	
		КонецЕсли;
		
		ПрочитаноСтрок = ПрочитаноСтрок + 1;
		
		МассивЗначенийСвойств = СтрРазделить(СледующаяСтрока,",",Ложь);
		// Первая всегда дата
		ДатаСобытияСтрокой = МассивЗначенийСвойств[0];
		ДатаСобытияСтрокой = СтрЗаменить(ДатаСобытияСтрокой,"""","");
		ДатаСобытияСтрокой = СтрЗаменить(ДатаСобытияСтрокой,"/","#");
		ДатаСобытияСтрокой = СтрЗаменить(ДатаСобытияСтрокой,":","#");
		ДатаСобытияСтрокой = СтрЗаменить(ДатаСобытияСтрокой,".","#");
		ДатаСобытияСтрокой = СтрЗаменить(ДатаСобытияСтрокой," ","#");
		
		Попытка
			МассивЧастейДаты = СтрРазделить(ДатаСобытияСтрокой,"#",Ложь);
			Год = Число(МассивЧастейДаты[2]);
			Месяц = Число(МассивЧастейДаты[0]);
			День = Число(МассивЧастейДаты[1]);
			Час = Число(МассивЧастейДаты[3]);
			Минута = Число(МассивЧастейДаты[4]);
			Секунда = Число(МассивЧастейДаты[5]);
			ДатаСобытия = Дата(Год,Месяц,День,Час,Минута,Секунда);
		Исключение
		КонецПопытки;
		
		СтруктураЗаписи.НомерСтрокиФайла = ПрочитаноСтрок;
		//СтруктураЗаписи.ТипСобытия = ТипСобытия;
		СтруктураЗаписи.ДатаСобытия = ДатаСобытия;
		//СтруктураЗаписи.ДатаСобытияМкс = Число(ДолиСекунды);
		//СтруктураЗаписи.УровеньСобытия = Число(УровеньСобытия);
		//СтруктураЗаписи.ДлительностьМкс = Число(Длительность);
		СтруктураЗаписи.КлючевыеСвойства.Очистить();	
		
		
		Для ш=1 по МассивЗначенийСвойств.ВГраница() Цикл
			
			ИмяСвойства = МассивИменСвойств[ш]; 			
			
			Свойство = СправочникиСерверПовтИсп.ПолучитьСвойство(ИмяСвойства);
			
			Если СтруктураЗаписи.КлючевыеСвойства.Получить(Свойство) = Неопределено Тогда 
				
				ЗначениеСвойства = 0;
				
				Попытка
					
					// очень маленькие будут 0
					Если Найти(МассивЗначенийСвойств[ш],"e-") Тогда
						ЗначениеСвойства = 0;
					Иначе
						ЗначениеСвойства = Число(СтрЗаменить(МассивЗначенийСвойств[ш],"""",""));
					КонецЕсли;
				
				Исключение
					
					ЗаписьЖурналаРегистрации("Ошибка загрузки",УровеньЖурналаРегистрации.Ошибка,,,"Проблема преобразования значение"+МассивЗначенийСвойств[ш]+Символы.ПС+СтрокаТекста);
					
				КонецПопытки;
				//ЗначениеСвойства = ЗначениеСвойстваБезЭкранирования(Совпадение.SubMatches.Item(1));
				
				Отказ = Ложь;	
				Если Отказ = Ложь Тогда
					СтруктураЗаписи.КлючевыеСвойства.Вставить(Свойство, ЗначениеСвойства);
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
		СтруктураЗаписи.ВсеСвойства = СтрокаТекста; //сохраним в оригинале
		
		
		Справочники.СобытияЗамера.ЗаписатьСобытиеЧисло(СтруктураЗаписи);
		
		СтрокаТекста = СледующаяСтрока;
	КонецЦикла;
	
	Текст.Закрыть();
	
	// Обновление инфорации о количестве прочитанных строк
	РегистрыСведений.СостояниеЧтения.УстановитьСостояние(
		ФайлЗамера, 
		ПериодФайла,
		ПрочитаноСтрок, 
		ДатаНачалаЧтения,
		РазмерФайла);

	Возврат 0;
КонецФункции

#КонецОбласти

#Область ЧтениеВСправочникЖурнала

Функция ПолучитьСтруктуруЗаписиСправочник() Экспорт
	//структура записи журнала
	СтруктураЗаписи = Новый Структура;
	СтруктураЗаписи.Вставить("Владелец");
	СтруктураЗаписи.Вставить("Файл");
	СтруктураЗаписи.Вставить("НомерСтрокиФайла");
	СтруктураЗаписи.Вставить("ДатаСобытия"); 
	СтруктураЗаписи.Вставить("ДатаСобытияМкс");
	СтруктураЗаписи.Вставить("ДлительностьМкс");
	СтруктураЗаписи.Вставить("ТипСобытия");
	СтруктураЗаписи.Вставить("УровеньСобытия");
	СтруктураЗаписи.Вставить("ВсеСвойства");
	СтруктураЗаписи.Вставить("ТипПроцесса");
	СтруктураЗаписи.Вставить("Ключ");
	СтруктураЗаписи.Вставить("КлючевыеСвойства", Новый Соответствие);
	Возврат СтруктураЗаписи;
КонецФункции

Функция РазобратьФайлВСправочник(Замер, ИмяФайлаДляРазбора) Экспорт
	//инициализация фильтров
	РеквизитыЗамера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Замер, "ФильтрТипСобытия,ФильтрСвойстваСобытия,ФильтрСвойстваСобытияКроме,ФильтрДлительность");
	РеквизитыЗамера.Вставить("ФильтрТипСобытия", РеквизитыЗамера.ФильтрТипСобытия.Получить());
	ЕстьФильтрТипСобытия = РеквизитыЗамера.ФильтрТипСобытия<>Неопределено И РеквизитыЗамера.ФильтрТипСобытия.Количество()>0;
	ЕстьФильтрСвойстваСобытия = ЗначениеЗаполнено(РеквизитыЗамера.ФильтрСвойстваСобытия);
	ЕстьФильтрДлительность = ЗначениеЗаполнено(РеквизитыЗамера.ФильтрДлительность); 
	
	ФайлЗамера = Справочники.ФайлыЗамера.ПолучитьФайлПоПолномуИмени(Замер, ИмяФайлаДляРазбора);
	
	//еще раз проверим прочитан полностью
	СостояниеЧтения = РегистрыСведений.СостояниеЧтения.ПолучитьСостояние(ФайлЗамера);
	Если СостояниеЧтения.ЧтениеЗавершено Тогда
		Возврат 0;
	КонецЕсли;		
	
	//пропуск пустых файлов
	ФайлТЖ = Новый Файл(ИмяФайлаДляРазбора);
	//ФС фикс 10.06.2020 проверка наличия файла
	Если НЕ ФайлТЖ.Существует() Тогда
		РегистрыСведений.СостояниеЧтения.УстановитьСостояниеОшибки(ФайлЗамера, "Файл не найден");
		Возврат 0;
	КонецЕсли;
	
	РазмерФайла = ФайлТЖ.Размер();
	Если РазмерФайла <=3 Тогда
		Возврат 0;
	КонецЕсли;
	
	ПериодФайла = ОбновлениеДанныхРегламентное.ПолучитьПериодПоИмениФайла(ФайлТЖ.ИмяБезРасширения);
	Процесс = ОбновлениеДанныхРегламентное.ПолучитьПроцессПоИмениФайла(ИмяФайлаДляРазбора);
	
	ДатаНачалаЧтения = ТекущаяДата();
	ДатаПрочитанныхДанных = Неопределено;
	
	Текст = Новый ЧтениеТекста(ИмяФайлаДляРазбора, КодировкаТекста.UTF8, Символы.ВК + Символы.ПС, "", Ложь);
	
	ПозиционироватьЧтениеНаСтрокуФайла(Текст, СостояниеЧтения.ПрочитаноСтрок);

	//продолжаем чтение с позиции СостояниеЧтения.ПрочитаноСтрок
	ПрочитаноСтрок = СостояниеЧтения.ПрочитаноСтрок;
	СтрокаТекста = Текст.ПрочитатьСтроку();
	Если СтрокаТекста = Неопределено Тогда
		
		//может быть прочитано строк не поменялось а полностью поменялось 
		РегистрыСведений.СостояниеЧтения.УстановитьСостояние(
			ФайлЗамера, 
			ПериодФайла,
			ПрочитаноСтрок, 
			ДатаНачалаЧтения, 
			РазмерФайла);		
		Возврат 0;
	КонецЕсли;
	ПрочитаноСтрок = ПрочитаноСтрок + 1;
	
	//регэксп объекты
	Анализатор = ПолучитьАнализаторНачалаСтроки();
	АнализаторСвойств = ПолучитьАнализаторСвойств();
	АнализаторДвойныеКавычкиВДвойныхКавычках = ПолучитьАнализаторКавычкиВКавычках();
	ЗаменительДвойныхКавычек = "ЁЁ";	
	ЗаменительДвойныхОдинарныхКавычек = "~Ё~Ё";
	АнализаторОтборПоСвойствам = АнализаторСоздать(РеквизитыЗамера.ФильтрСвойстваСобытия);
	
	//часть реквизитов будет одинакова для всего файла
	СтруктураЗаписи = ПолучитьСтруктуруЗаписиСправочник();
	СтруктураЗаписи.Владелец = Замер;
	СтруктураЗаписи.Файл = ФайлЗамера;
	СтруктураЗаписи.ТипПроцесса = Процесс;
	
	Пока СтрокаТекста <> Неопределено Цикл
		
		// Проверяем, является ли следующая строка начальной строкой журнала
		СледующаяСтрока = Текст.ПрочитатьСтроку();
		
		Если СледующаяСтрока <> Неопределено Тогда
			ПрочитаноСтрок = ПрочитаноСтрок + 1;
			Совпадения = Неопределено;
			Попытка
				Совпадения = АнализаторНайтиВсе(Анализатор , СледующаяСтрока, 1);
			Исключение         
				ТекстОшибки = ОписаниеОшибки();
				ЗаписьЖурналаРегистрации("РазобратьФайлВСправочник",УровеньЖурналаРегистрации.Ошибка,,Замер,ТекстОшибки);
			КонецПопытки;
			Если Совпадения=Неопределено ИЛИ Совпадения.Count() = 0 Тогда
				// если следующая строка не соответствует шаблону - добавляем ее к текущей строке и пытаемся распознать объединенную часть
				СтрокаТекста = СтрокаТекста + Символы.ПС + СледующаяСтрока; //#11 сохраняем переносы строк
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Совпадения = АнализаторНайтиВсе(Анализатор , СтрокаТекста, 6);
		Если Совпадения.Количество() = 1 Тогда
			Совпадение = Совпадения[0];
			Минуты = Число(Совпадение[0]);
			Секунды = Число(Совпадение[1]);
			ДолиСекунды = Число(Совпадение[2]);
			Длительность = Число(Совпадение[3]);
			ИмяСобытия = Совпадение[4];
			УровеньСобытия = Совпадение[5];
		Иначе          
			ТекстОшибки = "ERROR - Нет соответствия шаблону! "+Символы.ПС;
			ТекстОшибки = ТекстОшибки+" Имя файла: "+ИмяФайлаДляРазбора+Символы.ПС;
			ТекстОшибки = ТекстОшибки+" Прочитано строк: " + ПрочитаноСтрок+Символы.ПС;
			ТекстОшибки = ТекстОшибки+" Текст: " + Лев(СтрокаТекста,300)+"...";
			ЗаписьЖурналаРегистрации("РазобратьФайлВСправочник",УровеньЖурналаРегистрации.Ошибка,,Замер,ТекстОшибки);
			ОбработатьСбойныйЗамер(СтрокаТекста,СтруктураЗаписи,ПрочитаноСтрок,ПериодФайла);
			СтрокаТекста = СледующаяСтрока;
			Продолжить;
		КонецЕсли;
		
		//фильтр длительность
		Если ЕстьФильтрДлительность 
			И Длительность < РеквизитыЗамера.ФильтрДлительность Тогда
			СтрокаТекста = СледующаяСтрока;
			Продолжить;
		КонецЕсли;
		
		//фильтр тип события
		ТипСобытия = СправочникиСерверПовтИсп.ПолучитьСобытие(ИмяСобытия);
		Если ЕстьФильтрТипСобытия 
			И РеквизитыЗамера.ФильтрТипСобытия.НайтиПоЗначению(ТипСобытия)=Неопределено Тогда
			СтрокаТекста = СледующаяСтрока;
			Продолжить;
		КонецЕсли;
		
		//фильтр свойства события. пропуск если совпадает и "кроме", и если не совпадает и не "кроме"
		Если ЕстьФильтрСвойстваСобытия 
			И АнализаторПроверить(АнализаторОтборПоСвойствам, СтрокаТекста) = РеквизитыЗамера.ФильтрСвойстваСобытияКроме Тогда
			СтрокаТекста = СледующаяСтрока;
			Продолжить;
		КонецЕсли;
		
		СтруктураЗаписи.НомерСтрокиФайла = ПрочитаноСтрок;
		СтруктураЗаписи.ТипСобытия = ТипСобытия;
		СтруктураЗаписи.ДатаСобытия = ПериодФайла + Секунды + (Минуты * 60);
		СтруктураЗаписи.ДатаСобытияМкс = Число(ДолиСекунды);
		СтруктураЗаписи.УровеньСобытия = Число(УровеньСобытия);
		СтруктураЗаписи.ДлительностьМкс = Число(Длительность);
		СтруктураЗаписи.КлючевыеСвойства.Очистить();
		ДатаПрочитанныхДанных = СтруктураЗаписи.ДатаСобытия;

		//TODO: исправить ошибку разбора двойной одинарной кавычки
		// пока заменой делаем
		ЗаменитьОдитнарныеДвойныеКавычкиВОдинарныхДвойныхКавычках = Найти(СтрокаТекста,"''");
		Если ЗаменитьОдитнарныеДвойныеКавычкиВОдинарныхДвойныхКавычках Тогда
			СтрокаТекста = СтрЗаменить(СтрокаТекста, "''", ЗаменительДвойныхОдинарныхКавычек);			
		КонецЕсли;
		
		ЗаменитьДвойныеКавычкиВДвойныхКавычках = АнализаторПроверить(АнализаторДвойныеКавычкиВДвойныхКавычках, СтрокаТекста);
		Если ЗаменитьДвойныеКавычкиВДвойныхКавычках Тогда
			СтрокаТекста = СтрЗаменить(СтрокаТекста, """""", ЗаменительДвойныхКавычек);
		КонецЕсли;
		Совпадения = АнализаторНайтиВсе(АнализаторСвойств, СтрокаТекста, 2);
		Если Совпадения.Количество() <> 0 Тогда

			//ТекстЗначениеСвойств = "";
			
			Для Сч = 0 По Совпадения.Количество() - 1 Цикл
				Совпадение = Совпадения[Сч];
				ИмяСвойства = Совпадение[0];
				Если ИмяСвойстваЯвляетсяОшибочным(ИмяСвойства)=Истина Тогда
					Свойство = Справочники.Свойства.ОшибкаРазбора;
				Иначе
					Свойство = СправочникиСерверПовтИсп.ПолучитьСвойство(ИмяСвойства);
				КонецЕсли;				
				//при ошибке разбора имя свойства может быть неопределено. такое свойство не загружать
				Если НЕ ЗначениеЗаполнено(Свойство) Тогда
					Продолжить;
				КонецЕсли; 
				
				Если СтруктураЗаписи.КлючевыеСвойства.Получить(Свойство) = Неопределено Тогда 
				 
					ЗначениеСвойства = ЗначениеСвойстваБезЭкранирования(Совпадение[1]);
					Если ЗаменитьДвойныеКавычкиВДвойныхКавычках Тогда
						//обратно заменяем на ОДНУ двойную кавычку
						ЗначениеСвойства = СтрЗаменить(ЗначениеСвойства, ЗаменительДвойныхКавычек, """""");
					КонецЕсли;
					
					Если ЗаменитьОдитнарныеДвойныеКавычкиВОдинарныхДвойныхКавычках Тогда
						//обратно заменяем на ОДНУ двойную кавычку
						ЗначениеСвойства = СтрЗаменить(ЗначениеСвойства, ЗаменительДвойныхОдинарныхКавычек, "''");
					КонецЕсли;
					
					Отказ = Ложь;	
					ВыполнитьНормализациюЗначенияСвойства(СтруктураЗаписи, Свойство, ЗначениеСвойства, Отказ);
					Если Отказ = Ложь Тогда
						СтруктураЗаписи.КлючевыеСвойства.Вставить(Свойство, ЗначениеСвойства);
					КонецЕсли;

					//ТекстЗначениеСвойств = ТекстЗначениеСвойств + ИмяСвойства +" : "+ ЗначениеСвойства + Символы.ПС;
				КонецЕсли;
			КонецЦикла;
			СтруктураЗаписи.ВсеСвойства = СтрокаТекста; //сохраним в оригинале
			СтруктураЗаписи.ВсеСвойства = СтрЗаменить(СтруктураЗаписи.ВсеСвойства, ЗаменительДвойныхКавычек, """""");
			СтруктураЗаписи.ВсеСвойства = СтрЗаменить(СтруктураЗаписи.ВсеСвойства, ЗаменительДвойныхОдинарныхКавычек, "''");
		КонецЕсли;
		
		Справочники.СобытияЗамера.ЗаписатьСобытиеЧисло(СтруктураЗаписи);
		
		СтрокаТекста = СледующаяСтрока;
	КонецЦикла;
	
	Текст.Закрыть();
	
	// Обновление инфорации о количестве прочитанных строк
	РегистрыСведений.СостояниеЧтения.УстановитьСостояние(
		ФайлЗамера, 
		ПериодФайла,
		ПрочитаноСтрок, 
		ДатаНачалаЧтения,
		РазмерФайла,
		ДатаПрочитанныхДанных);

	Возврат 0;
КонецФункции

Процедура ОбработатьСбойныйЗамер(СтрокаТекста,СтруктураЗаписи,ПрочитаноСтрок,ПериодФайла)
	
	СтруктураЗаписи.НомерСтрокиФайла = ПрочитаноСтрок;
	СтруктураЗаписи.ТипСобытия = "";
	СтруктураЗаписи.ДатаСобытия = ПериодФайла;
	СтруктураЗаписи.ДатаСобытияМкс = 0;
	СтруктураЗаписи.УровеньСобытия = 0;
	СтруктураЗаписи.ДлительностьМкс = 0;
	СтруктураЗаписи.КлючевыеСвойства.Очистить();
	
	Справочники.СобытияЗамера.ЗаписатьСобытиеЧисло(СтруктураЗаписи);
КонецПроцедуры

Процедура ВыполнитьНормализациюЗначенияСвойства(СтруктураЗаписи, Свойство, ЗначениеСвойства, Отказ)
	НастройкиНормализации = СправочникиСерверПовтИсп.РеквизитыСвойства(Свойство);
	Если НастройкиНормализации.НормализацияЗначения Тогда
		Выполнить(НастройкиНормализации.ТекстФункцииНормализации);
	КонецЕсли;
КонецПроцедуры

// TODO: Дописать нормальную функцию проверки правильности разбора
Функция ИмяСвойстваЯвляетсяОшибочным(ИмяСвойства)
	
	Если Найти(ИмяСвойства,"(") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Найти(ИмяСвойства,")") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

#КонецОбласти

#Область ЧтениеВСправочникПроизвольный

Функция РазобратьФайлВСправочникПроизвольный(Замер, СтруктураПараметров) Экспорт
	
	
	//инициализация фильтров
	ДополнительнаяОбработка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Замер, "ДополнительнаяОбработка");

	//выполним вызов функции обработки
	Попытка
		
		ХранилищеОбработки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительнаяОбработка,"ХранилищеОбработки");
		ДвоичныеДанные = ХранилищеОбработки.Получить();
		АдресОбработки = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		ЗащитаОтОпасныхДействий = новый ОписаниеЗащитыОтОпасныхДействий;
		ЗащитаОтОпасныхДействий.ПредупреждатьОбОпасныхДействиях = Ложь;
		ИмяВнешнейОбработки = ВнешниеОбработки.Подключить(АдресОбработки,,Ложь,ЗащитаОтОпасныхДействий);
		ВнешняяОбработка = ВнешниеОбработки.Создать(ИмяВнешнейОбработки);
		ВнешняяОбработка.ВыполнитьЗагрузкуДанных(Замер,ДополнительнаяОбработка);	
		
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("ЧтениеВСправочник",УровеньЖурналаРегистрации.Ошибка,Неопределено,Замер,ТекстОшибки);
	КонецПопытки;

	Возврат 0;
КонецФункции


#КонецОбласти