#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаЗамера) Тогда
		ДатаЗамера = ТекущаяДата();
	КонецЕсли;
	
	УстановитьСостояниеРЗ(Отказ);
КонецПроцедуры

Процедура УстановитьСостояниеРЗ(Отказ)
	Перем ЗагружатьАвтоматически, НастройкиРЗ;
	
	//для помеченных автоматически отключаем
	Если ПометкаУдаления Тогда
		ДополнительныеСвойства.Вставить("ЗагружатьАвтоматически", Ложь);
	КонецЕсли;
	
	//для других случаев если не задан параметр, пропуск
	Если ДополнительныеСвойства.Свойство("ЗагружатьАвтоматически", ЗагружатьАвтоматически) = Ложь Тогда
		Возврат;
	КонецЕсли;
	 
	ПустойУИД = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"); 
	Если ЗагружатьАвтоматически = Истина Тогда
		СсылкаОбъекта = Ссылка;
		Если ЭтоНовый() Тогда
			СсылкаОбъекта = ПолучитьСсылкуНового();
			Если НЕ ЗначениеЗаполнено(СсылкаОбъекта) Тогда
				СсылкаОбъекта = Справочники.Замеры.ПолучитьСсылку();
				УстановитьСсылкуНового(СсылкаОбъекта);
			КонецЕсли;
		КонецЕсли; 
		
		Если ИдентификаторРЗ = ПустойУИД Тогда
			РЗ = РегламентныеЗадания.СоздатьРегламентноеЗадание("Автозагрузка");			
		Иначе
			РЗ = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторРЗ);
			Если РЗ = Неопределено Тогда
				РЗ = РегламентныеЗадания.СоздатьРегламентноеЗадание("Автозагрузка");
			КонецЕсли;
		КонецЕсли;
		
		ДополнительныеСвойства.Свойство("НастройкиРЗ", НастройкиРЗ);
		РЗ.Расписание = НастройкиРЗ;
		РЗ.Наименование = СтрШаблон("Автозагрузка: %1", Наименование);
		РЗ.Использование = Истина;
		РЗ.Параметры.Очистить();
		РЗ.Параметры.Добавить(СсылкаОбъекта);
		РЗ.Записать();
		ИдентификаторРЗ = РЗ.УникальныйИдентификатор;
	Иначе
		Если ИдентификаторРЗ <> ПустойУИД Тогда
			РЗ = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторРЗ);
			Если РЗ <> Неопределено Тогда
				РЗ.Удалить();
			КонецЕсли;
			ИдентификаторРЗ = ПустойУИД;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецЕсли