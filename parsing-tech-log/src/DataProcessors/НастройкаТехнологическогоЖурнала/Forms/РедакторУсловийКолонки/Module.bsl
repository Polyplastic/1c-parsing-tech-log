
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем ОбъектОбработки, Текст, ЭлементСписка, СписокТипов, УсловияСобытия, Условие, СтрокаДанных;
	
	ОбъектОбработки = РеквизитФормыВЗначение("Объект");
	МетаПуть = ОбъектОбработки.Метаданные().ПолноеИмя();
	Событие = Параметры.Событие;
	Колонка = Параметры.Колонка;
	Текст = НСтр("ru = 'Редактирование условий для свойства <%1%> события <%2%>'; sys = ''", "ru");
	Текст = СтрЗаменить(Текст, "%1%", СокрЛП(Колонка));
	Заголовок = СтрЗаменить(Текст, "%2%", СокрЛП(Событие));
	// зададим список выбора для списка колонок
	Для каждого ЭлементСписка из Параметры.СписокКолонок Цикл
		СписокКолонок.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	СписокКолонок.СортироватьПоПредставлению();
	// установим список типов
	СписокТипов = Новый Структура;
	// скопируем таблицу типов из параметра в нашу форму
	Для каждого ТипКолонки Из Параметры.СписокТипов Цикл
		СписокТипов.Вставить(ТипКолонки.Ключ, ?(ТипЗнч(ТипКолонки.Значение) = Тип("СписокЗначений"), ТипКолонки.Значение.Скопировать(), ТипКолонки.Значение));
	КонецЦикла; 
	// установим текущие условия
	Если Параметры.ВсеУсловия <> Неопределено Тогда
		УсловияСобытия = Параметры.ВсеУсловия.НайтиСтроки(Новый Структура("ВедущаяКолонка,Событие", Колонка, Событие));
		Для каждого Условие Из УсловияСобытия Цикл
			СтрокаДанных = УсловияЖурнала.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДанных, Условие);
			СтрокаДанных.КолонкаПредставление = ПредставлениеКолонки(СтрокаДанных.Колонка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Перем Данные, Условие, Элемент;
	// сложим условия в транспортабельную структуру
	Данные = Новый Массив;
	Для каждого Условие Из УсловияЖурнала Цикл
		Элемент = Новый Структура("Колонка, Условие, Значение, ВедущаяКолонка, Событие, КолонкаПредставление");
		ЗаполнитьЗначенияСвойств(Элемент, Условие);
		Данные.Добавить(Элемент);
	КонецЦикла;
	Закрыть(Данные);
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеКолонки(ИмяКолонки)
	
	Перем Результат;
	
	Результат = СписокКолонок.НайтиПоЗначению(ИмяКолонки);
	Если Результат = Неопределено Тогда
		Возврат "<?>";
	Иначе
		Возврат Результат.Представление;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РедактируемоеПоле(ИмяЭлемента)

	Если ИмяЭлемента = "УсловияЖурналаКолонкаПредставление" Тогда
		Возврат "Свойство";
	ИначеЕсли ИмяЭлемента = "УсловияЖурналаУсловие" Тогда
		Возврат "Условие";
	ИначеЕсли ИмяЭлемента = "УсловияЖурналаЗначение" Тогда
		Возврат "Значение";
	КонецЕсли;
	Возврат "";

КонецФункции

&НаКлиенте
Процедура УсловияЖурналаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Перем ПараметрыФормы, ОбратныйВызов;
	Отказ = Истина;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Свойство", "");
	ПараметрыФормы.Вставить("Условие", "");
	ПараметрыФормы.Вставить("Значение", "");
	ПараметрыФормы.Вставить("СписокТипов", СписокТипов);
	ПараметрыФормы.Вставить("СписокКолонок", СписокКолонок);
	ПараметрыФормы.Вставить("ТекущаяКолонка", "");
	ОбратныйВызов = Новый ОписаниеОповещения("УсловияЖурналаПередНачаломДобавленияЗавершение", ЭтотОбъект);
	ОткрытьФорму(МетаПуть + ".Форма.РедакторУсловия", ПараметрыФормы, , , , , ОбратныйВызов);
	
КонецПроцедуры

&НаКлиенте
Процедура УсловияЖурналаПередНачаломДобавленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Перем СтрокаСписка;
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		СтрокаСписка = УсловияЖурнала.Добавить();
		СтрокаСписка.Колонка = Результат.Свойство;
		СтрокаСписка.КолонкаПредставление = ПредставлениеКолонки(Результат.Свойство);
		СтрокаСписка.Условие = Результат.Условие;
		СтрокаСписка.Значение = Результат.Значение;
		СтрокаСписка.Событие = Событие;
		СтрокаСписка.ВедущаяКолонка = Колонка;
		Элементы.УсловияЖурнала.ТекущаяСтрока = СтрокаСписка.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УсловияЖурналаПередНачаломИзменения(Элемент, Отказ)
	
	Перем ТекущиеДанные, ПараметрыФормы, ОбратныйВызов;
	
	Отказ = Истина;

	ТекущиеДанные = Элементы.УсловияЖурнала.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Свойство", ТекущиеДанные.Колонка);
	ПараметрыФормы.Вставить("Условие", ТекущиеДанные.Условие);
	ПараметрыФормы.Вставить("Значение", ТекущиеДанные.Значение);
	ПараметрыФормы.Вставить("СписокТипов", СписокТипов);
	ПараметрыФормы.Вставить("СписокКолонок", СписокКолонок);
	ПараметрыФормы.Вставить("ТекущаяКолонка", РедактируемоеПоле(Элементы.УсловияЖурнала.ТекущийЭлемент.Имя));
	ОбратныйВызов = Новый ОписаниеОповещения("УсловияЖурналаПередНачаломИзмененияЗавершение", ЭтотОбъект);
	ОткрытьФорму(МетаПуть + ".Форма.РедакторУсловия", ПараметрыФормы, , , , , ОбратныйВызов);
	
КонецПроцедуры

&НаКлиенте
Процедура УсловияЖурналаПередНачаломИзмененияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Перем ТекущиеДанные;
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ТекущиеДанные = Элементы.УсловияЖурнала.ТекущиеДанные;
		ТекущиеДанные.Колонка = Результат.Свойство;
		ТекущиеДанные.КолонкаПредставление = ПредставлениеКолонки(Результат.Свойство);
		ТекущиеДанные.Условие = Результат.Условие;
		ТекущиеДанные.Значение = Результат.Значение;
	КонецЕсли;
	
КонецПроцедуры
