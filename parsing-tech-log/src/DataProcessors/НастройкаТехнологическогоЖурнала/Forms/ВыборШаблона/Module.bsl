
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем ОбъектОбработки, Номер, ИмяШаблон, Макет, НовыйШаблон, Представление, Описание;
	
	// соберем все шаблоны, которые есть в обработке с именами "Шаблон*"
	ОбъектОбработки = РеквизитФормыВЗначение("Объект");
	Номер = 1;
	Пока Истина Цикл
		ИмяШаблона = "Шаблон"+Строка(Номер);
		Попытка
			Макет = ОбъектОбработки.ПолучитьМакет(ИмяШаблона);
			Номер = Номер + 1;
		Исключение
			// закончим поиски после номера без макета
			Прервать;
		КонецПопытки;
		// нашли шаблон, попробуем его разобрать исключительно по фиксированным позициям
		НовыйШаблон = СписокШаблонов.Добавить();
		НовыйШаблон.Имя = ИмяШаблона;
		Представление = Макет.ПолучитьСтроку(2);
		НовыйШаблон.Представление = ?(ПустаяСтрока(Представление), ИмяШаблона, Представление);
		Описание = Макет.ПолучитьСтроку(4);
		НовыйШаблон.Описание = ?(ПустаяСтрока(Описание), НовыйШаблон.Представление, Описание);
		НовыйШаблон.Внешний = Ложь;
		НовыйШаблон.Картинка = БиблиотекаКартинок.СтандартнаяНастройкаКомпоновкиДанных;
	КонецЦикла;
	ПутьКШалонам = Параметры.ПутьКШаблонам;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Перем Докмуент, Файлы, Представление, Описание, НовыйШаблон;
#Если ВебКлиент Тогда
	Элементы.КонтекстноеМенюСписокШаблоновУдалитьШаблонСДиска.Доступность = Ложь;
#КонецЕсли
#Если ТонкийКлиент Тогда
 
 	// для тонкого клиента попробуем прочитать шаблоны в каталоге конфигурационных файлов
	// файл шаблона имеет следующий шаблон - config*.lct
	// вместо звездочки по умолчанию будут подставляться цифры (при сохранении)
	Если НЕ ПустаяСтрока(ПутьКШалонам) Тогда
		Документ = Новый ТекстовыйДокумент;
		Файлы = НайтиФайлы(ПутьКШалонам, "config*.lct", Истина);
		// Теперь попытаемся прочитать все найденные файлы - может и наше есть
		Для каждого Файл Из Файлы Цикл
			Документ.Прочитать(Файл.ПолноеИмя);
			Если НРег(Документ.ПолучитьСтроку(1)) <> "имя:" И НРег(Документ.ПолучитьСтроку(1)) <> "описание:" Тогда
				// считаем, что это не наш файл
				Продолжить;
			КонецЕсли;
			// нашли шаблон, попробуем его разобрать исключительно по фиксированным позициям
			НовыйШаблон = СписокШаблонов.Добавить();
			НовыйШаблон.Имя = Файл.ПолноеИмя;
			Представление = Документ.ПолучитьСтроку(2);
			НовыйШаблон.Представление = ?(ПустаяСтрока(Представление), Файл.ИмяБезРасширения, Представление);
			Описание = Документ.ПолучитьСтроку(4);
			НовыйШаблон.Описание = ?(ПустаяСтрока(Описание), НовыйШаблон.Представление, Описание);
			НовыйШаблон.Внешний = Истина;
			НовыйШаблон.Картинка = Новый Картинка;
		КонецЦикла;
	КонецЕсли;
#КонецЕсли
	СписокШаблонов.Сортировать("Представление");

КонецПроцедуры

&НаКлиенте
Процедура СписокШаблоновВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Закрыть(Элемент.ТекущиеДанные.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура Завершить(Команда)
	
	Закрыть(Элементы.СписокШаблонов.ТекущиеДанные.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьШаблонСДиска(Команда)
	
	Перем ОписаниеШаблона, Текст, ОбратныйВызов, ПараметрыВызова;
	
#Если НЕ ВебКлиент Тогда
	// Удалим шаблон, если он не находится в самой обработке
	ОписаниеШаблона = Элементы.СписокШаблонов.ТекущиеДанные;
	Если ОписаниеШаблона = Неопределено ИЛИ НЕ ОписаниеШаблона.Внешний Тогда
		Возврат;
	КонецЕсли;
	Текст = НСтр("ru = 'Вы действительно хотите удалить шаблон ""%1%""?'; sys = ''", "ru");
	Текст = СтрЗаменить(Текст, "%1%", ОписаниеШаблона.Представление);
	ПараметрыВызова = Новый Структура;
	ПараметрыВызова.Вставить("Имя", ОписаниеШаблона.Имя);
	ПараметрыВызова.Вставить("Описание", ОписаниеШаблона);
	ОбратныйВызов = Новый ОписаниеОповещения("УдалитьШаблонСДискаЗавершение", ЭтотОбъект, ПараметрыВызова);
	ПоказатьВопрос(ОбратныйВызов, Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура УдалитьШаблонСДискаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УдалитьФайлы(ДополнительныеПараметры.Имя);
		СписокШаблонов.Удалить(ДополнительныеПараметры.Описание);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокШаблоновПриАктивизацииСтроки(Элемент)
	
	Перем ОписаниеШаблона;
	
	ОписаниеШаблона = Элементы.СписокШаблонов.ТекущиеДанные;
	Если ОписаниеШаблона = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Элементы.КонтекстноеМенюСписокШаблоновУдалитьШаблонСДиска.Доступность = ОписаниеШаблона.Внешний;
	
КонецПроцедуры
